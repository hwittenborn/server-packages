# Maintainer: Hunter Wittenborn <hunter@hunterwittenborn.com>
pkgbase=frp
pkgname=('frps' 'frpc')
pkgver=0.49.0
pkgrel=1
pkgdesc='A fast reverse proxy to help you xpose a local server behind a NAT or firewall to the internet'
arch=('any')
makedepends=('golang-go')
license=('Apache-2.0')
url='https://github.com/fatedier/frp'

source=("https://github.com/fatedier/frp/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('8ff92d4f763d596bee35efe17f0729d36e584b93c49a7671cebde4bb318b458f')

build() {
    cd "${pkgbase}-${pkgver}/"
    make build
}

package_frps() {
    cd "${pkgbase}-${pkgver}/"
    install -Dm 755 bin/frps "${pkgdir}/usr/bin/frps"
}

package_frpc() {
    cd "${pkgbase}-${pkgver}/"
    install -Dm 755 bin/frpc "${pkgdir}/usr/bin/frpc"
}

# vim: set sw=4 expandtab:
